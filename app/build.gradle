apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

// Load local properties with API keys
def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

android {
    compileSdkVersion 31
    buildToolsVersion '30.0.3'
    defaultConfig {
        applicationId 'com.b_lam.resplash'
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 58
        versionName '3.3.7'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        buildConfigField("String", "RELEASE_APP_ID", localProperties['releaseAppId'])
        buildConfigField("String", "RELEASE_SECRET", localProperties['releaseSecret'])
        buildConfigField("String", "DEV_APP_ID", localProperties['devAppId'])
        buildConfigField("String", "DEV_SECRET", localProperties['devSecret'])
        buildConfigField("String", "GOOGLE_PLAY_LICENSE_KEY", localProperties['googlePlayLicenseKey'])
        renderscriptTargetApi 31 as int
        renderscriptSupportModeEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
        }
    }
    buildFeatures {
        viewBinding true
    }
    bundle {
        language {
            enableSplit = false
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lintOptions {
        disable 'MissingTranslation'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Support libraries
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.browser:browser:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.fragment:fragment-ktx:1.4.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.paging:paging-runtime-ktx:2.1.2'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime-ktx:2.7.1'

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-service:2.4.1'

    // Room
    implementation 'androidx.room:room-runtime:2.4.2'
    implementation 'androidx.room:room-ktx:2.4.2'
    kapt 'androidx.room:room-compiler:2.4.2'

    // Material components
    implementation 'com.google.android.material:material:1.6.0-beta01'

    // Billing
    implementation 'com.android.billingclient:billing:3.0.3'
    implementation 'com.android.billingclient:billing-ktx:3.0.3'

    // Kotlin coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'

    // OkHttp
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2'

    // Okio
    implementation 'com.squareup.okio:okio:2.10.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'

    // Moshi
    implementation 'com.squareup.moshi:moshi:1.13.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.13.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    implementation 'jp.wasabeef:glide-transformations:4.3.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    // Koin
    implementation 'io.insert-koin:koin-android:3.1.5'
    implementation 'io.insert-koin:koin-androidx-workmanager:3.1.4'

    // Firebase
    implementation 'com.google.firebase:firebase-analytics-ktx:20.1.2'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.9'
    implementation('com.google.firebase:firebase-inappmessaging-ktx:20.1.0', {
        exclude group: 'io.grpc', module: 'grpc-okhttp'
    })
    // Workaround for https://github.com/firebase/firebase-android-sdk/issues/2642
    implementation 'io.grpc:grpc-okhttp:1.44.1'
    implementation 'com.google.firebase:firebase-firestore-ktx:24.1.0'
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'

    // Play Core
    implementation 'com.google.android.play:core:1.10.3'
    implementation 'com.google.android.play:core-ktx:1.8.1'

    // View binding
    implementation 'com.github.kirich1409:viewbindingpropertydelegate:1.4.7'
    
    // Lottie
    implementation 'com.airbnb.android:lottie:4.2.2'

    // PhotoView
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    // Pager indicator
    implementation 'com.github.intik:overflow-pager-indicator:3.1.0'

    // PrettyTime
    implementation 'org.ocpsoft.prettytime:prettytime:4.0.4.Final'

    // Open source notices
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    // Muzei
    implementation 'com.google.android.apps.muzei:muzei-api:3.4.0'

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8'

    // Unit tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'

    // Room testing
    androidTestImplementation "androidx.room:room-testing:2.4.2"
}
